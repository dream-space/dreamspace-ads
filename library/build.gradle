plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'dreamspace.ads'
    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'GradleDependency', 'ExpiringTargetSdkVersion'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude: [])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'com.google.android.gms:play-services-appset:16.0.2'
    implementation 'com.google.android.gms:play-services-basement:18.1.0'

    //Ad Networks
    implementation 'com.google.android.gms:play-services-ads:21.5.0'
    implementation 'com.unity3d.ads:unity-ads:4.6.0'
    implementation 'com.ironsource.sdk:mediationsdk:7.2.7'

    //GDPR
    implementation 'com.google.android.ump:user-messaging-platform:2.0.0'
    implementation 'com.google.android.ads.consent:consent-library:1.0.8'
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/dream-space/dreamspace-ads")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "dream-space"
                artifactId = "dreamspace.ads"
                version = "1.0"
            }
        }
    }
}